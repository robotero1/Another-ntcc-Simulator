%% Programa de ntcc: Controlador de un semaforo vial.
%% Este es una adaptacion del ejemplo 4 presentado en "Models
%% and Emerging Trends of Concurrent Constraint Programming" por Carlos,
%% Olarte, Camilo Rueda y Frank D. Valencia, Springer Verlag, 2013.
%% En este ejemplo se modela el controlador de un semaforo. Suponga
%% que dicho controlador recibe senales del ambiente que le indican
%% cuando cambiar su estado. Por un lado esta la variable 'state' que
%% corresponde a los colores del semaforo, si state=1, entonces el
%% semaforo esta en rojo; si state=2, el semaforo esta en amarillo
%% y si state=3, en verde. Por otro lado, la variable 'change' es la
%% que determina cuando el semaforo debe cambiar de color, si change=0,
%% el semaforo permanece igual y si change=1 el semaforo cambia de color.
%% Corra el siguiente codigo y observe los resultados.
NtccSim
begin
declare state change domain = [1#3 0#1] tsim = 17
in
sem = bang (
	    par (
		 when (change == 1) do
		       sum ( when (state == 1) do next ( tell (state = 2))
	      		     when (state == 2) do next ( tell (state = 3))
	      		     when (state == 3) do next ( tell (state = 1)))
		 when (change == 0) do
		       sum ( when (state == 1) do next ( tell (state = 1))
			     when (state == 2) do next ( tell (state = 2))
			     when (state == 3) do next ( tell (state = 3)))
		 sum ( when (change == 0) do next ( tell (change = 1))
	      	       when (change == 1) do next ( tell (change = 0)))));
%% Condiciones iniciales:
tell (state = 1);
tell (change = 0);
sem
end
